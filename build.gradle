allprojects {

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    repositories {
        if(project.mavenUrl) {
            maven {
                allowInsecureProtocol = true
                url = project.mavenUrl
                credentials {
                    username = providers.gradleProperty("mavenUsername").get()
                    password = providers.gradleProperty("mavenPassword").get()
                }
            }
        }
        mavenCentral()
    }

    afterEvaluate {
        // java
        if(it.plugins.hasPlugin("java")) {
            it.extensions.configure(JavaPluginExtension) { java ->
                java.toolchain {
                    languageVersion.set(JavaLanguageVersion.of(17))
                }
            }
            it.compileJava.options.encoding = "UTF-8"
            it.tasks.withType(JavaCompile){
                options.encoding = 'UTF-8'
                options.compilerArgs += "-parameters"
            }
            it.tasks.withType(Jar) {
                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
            it.sourceSets.main.java {
                exclude "**/*.xml"
            }
            it.sourceSets.main.resources {
                srcDirs += ["src/main/java"]
                exclude "**/*.java"
            }
            it.sourceSets.test.java {
                exclude "**/*.xml"
            }
            it.sourceSets.test.resources {
                srcDirs += ["src/test/java"]
                exclude "**/*.java"
            }
            it.java {
                withSourcesJar()
            }
        }
        // junit test
        it.tasks.withType(Test).configureEach {
            useJUnitPlatform {
                excludeTags 'manual'
            }
            testLogging {
                events 'failed'
                exceptionFormat = 'full'
            }
        }
        // maven-publish
        if(it.plugins.hasPlugin('maven-publish')) {
            it.publishing {
                repositories {
                    maven {
                        allowInsecureProtocol = true
                        url = project.publishingMavenUrl
                        credentials {
                            username = providers.gradleProperty("publishingMavenUsername").get()
                            password = providers.gradleProperty("publishingMavenPassword").get()
                        }
                    }
                }
            }
        }
    }

}
